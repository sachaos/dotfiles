export TERM=xterm-256color

# Editor
export EDITOR='emacsclient -nw -a ""'

##########################################################
## peco functions
##########################################################

# peco history
function peco-select-history() {
    typeset tac
    if which tac > /dev/null; then
        tac=tac
    else
        tac='tail -r'
    fi
    BUFFER=$(fc -l -n 1 | eval $tac | peco --query "$LBUFFER")
    CURSOR=$#BUFFER
    zle redisplay
}
zle -N peco-select-history
bindkey '^r' peco-select-history

# peco find
function peco-find() {
    local current_buffer=$BUFFER
    # .git系など不可視フォルダは除外
    local selected_file="$(find . -maxdepth 5 ! -path "*/.git*" | grep -v '~$' | sort | peco | sed -e "s/)/\\\)/g" -e "s/(/\\\(/g" -e "s/\ /\\\ /g" -e "s/#/\\\#/g" | tr '\n' ' ')"
    if [ -n "$selected_file" ]; then
        BUFFER="${current_buffer} ${selected_file}"
        CURSOR=$#BUFFER
    fi
    zle clear-screen
}
zle -N peco-find
bindkey '^x^f' peco-find

# peco directory select
autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
zstyle ':chpwd:*' recent-dirs-max 5000 # cdrの履歴を保存する個数
zstyle ':chpwd:*' recent-dirs-default yes
zstyle ':completion:*' recent-dirs-insert both

function peco-cdr () {
    local selected_dir=$(cdr -l | awk '{print $2}' | peco)
    if [ -n "$selected_dir" ]; then
        local new_left="${LBUFFER} ${selected_dir} "
        BUFFER=${new_left}${RBUFFER}
        CURSOR=${#new_left}
    fi
}
zle -N peco-cdr
bindkey '^xi' peco-cdr

# peco kill
function peco-kill-process() {
  ps -ef | peco | awk '{print $2}' | xargs kill
  zle clear-screen
}
zle -N peco-kill-process
bindkey '^xk' peco-kill-process

# peco で github の branchを選択
function peco-branch () {
  local branch=$(git branch -a | peco | tr -d ' ' | tr -d '*')
  if [ -n "$branch" ]; then
    if [ -n "$LBUFFER" ]; then
      local new_left="${LBUFFER%\ } $branch"
    else
      local new_left="$branch"
    fi
    BUFFER=${new_left}${RBUFFER}
    CURSOR=${#new_left}
  fi
}
zle -N peco-branch
bindkey '^g^b' peco-branch

# pecoでgit add
function peco-select-gitadd() {
    local SELECTED_FILE_TO_ADD="$(git status --porcelain | \
                                  peco --query "$LBUFFER" | \
                                  awk -F ' ' '{print $NF}')"
    if [ -n "$SELECTED_FILE_TO_ADD" ]; then
        BUFFER="git add $(echo "$SELECTED_FILE_TO_ADD" | tr '\n' ' ')"
        CURSOR=$#BUFFER
    fi
    zle accept-line
    # zle clear-screen
}
zle -N peco-select-gitadd
bindkey "^g^a" peco-select-gitadd

# pecoでgemのファイルを選択
function peco-gem () {
  local file_path=$(gem which $(gem list -q | peco) 2>/dev/null)
  echo $file_path
  if [ -n "$file_path" ]; then
    if [ -n "$LBUFFER" ]; then
      local new_left="${LBUFFER%\ } $file_path"
    else
      local new_left="$file_path"
    fi
    BUFFER=${new_left}${RBUFFER}
    CURSOR=${#new_left}
  fi
}
zle -N peco-gem

####################
# todoist commands #
####################

# todoist find task
function peco-todoist-task () {
    local SELECTED_TASK="$(todoist list | peco | head -n1 | cut -d ' ' -f 1)"
    if [ -n "$SELECTED_TASK" ]; then
        if [ -n "$LBUFFER" ]; then
            local new_left="${LBUFFER%\ } $SELECTED_TASK"
        else
            local new_left="$SELECTED_TASK"
        fi
        BUFFER=${new_left}${RBUFFER}
        CURSOR=${#new_left}
    fi
}
zle -N peco-todoist-task
bindkey "^xtt" peco-todoist-task

# todoist find project
function peco-todoist-project () {
    local SELECTED_PROJECT="$(todoist projects | peco | head -n1 | cut -d ' ' -f 1)"
    if [ -n "$SELECTED_PROJECT" ]; then
        if [ -n "$LBUFFER" ]; then
            local new_left="${LBUFFER%\ } $SELECTED_PROJECT"
        else
            local new_left="$SELECTED_PROJECT"
        fi
        BUFFER=${new_left}${RBUFFER}
        CURSOR=${#new_left}
    fi
}
zle -N peco-todoist-project
bindkey "^xtp" peco-todoist-project

# todoist find labels
function peco-todoist-labels () {
    local SELECTED_LABELS="$(todoist labels | peco | cut -d ' ' -f 1 | tr '\n' ',' | sed -e 's/,$//')"
    if [ -n "$SELECTED_LABELS" ]; then
        if [ -n "$LBUFFER" ]; then
            local new_left="${LBUFFER%\ } $SELECTED_LABELS"
        else
            local new_left="$SELECTED_LABELS"
        fi
        BUFFER=${new_left}${RBUFFER}
        CURSOR=${#new_left}
    fi
}
zle -N peco-todoist-labels
bindkey "^xtl" peco-todoist-labels

# todoist close
function peco-todoist-close() {
    local SELECTED_ITEMS="$(todoist list | peco | cut -d ' ' -f 1 | tr '\n' ' ')"
    if [ -n "$SELECTED_ITEMS" ]; then
        BUFFER="todoist close $(echo "$SELECTED_ITEMS" | tr '\n' ' ')"
        CURSOR=$#BUFFER
    fi
    zle accept-line
}
zle -N peco-todoist-close
bindkey "^xtc" peco-todoist-close

#################################################
## various functions
#################################################

mkcd () {
    mkdir -p $1 && cd $1
}

# enterでいろいろ表示させるようにする

function do_enter () {
    if [ -n "$BUFFER" ]; then
        zle accept-line
        return 0
    fi
    echo
    pwd
    echo -e "\e[0;33m--- files ---\e[0m"
    ls
    if [ "$(git rev-parse --is-inside-work-tree 2> /dev/null)" = 'true' ]; then
        echo
        echo -e "\e[0;33m--- git status ---\e[0m"
        git status -sb
    fi
    echo
    echo
    echo
    zle reset-prompt
    return 0
}
zle -N do_enter
bindkey '^m' do_enter
